import com.purnaprasanth.buildsrc.Libs

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://oss.jfrog.org/artifactory/libs-release" }
    }

    dependencies {
        classpath(Libs.Plugins.androidGradlePlugin)
        classpath(Libs.Plugins.hiltPlugin)
        classpath(Libs.Plugins.versionChecker)

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.21"
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "com.github.ben-manes.versions" version "0.42.0"
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: "com.github.ben-manes.versions"
    detekt {
        config = files("${project.rootDir}/config/detekt/detekt.yml")
        parallel = true
    }
    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            def version = it.candidate.version
            def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { stableTag -> version.toUpperCase().contains(stableTag) }
            def regex = /^[0-9,.v-]+(-r)?$/
            return !stableKeyword && !(version ==~ regex)
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
